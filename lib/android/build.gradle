apply plugin: 'com.android.library'

project.ext {
    buildToolsVersion = rootProject.ext.has("buildToolsVersion") ? rootProject.ext.buildToolsVersion : '27.0.3'
    minSdkVersion = rootProject.ext.has("minSdkVersion") ? rootProject.ext.minSdkVersion : 19
    compileSdkVersion = rootProject.ext.has("compileSdkVersion") ? rootProject.ext.compileSdkVersion : 27
    targetSdkVersion = rootProject.ext.has("targetSdkVersion") ? rootProject.ext.targetSdkVersion : 25
    supportLibVersion = rootProject.ext.has("supportLibVersion") ? rootProject.ext.supportLibVersion  : '27.1.1'
}

android {
    namespace "com.wix.reactnativeuilib"
    compileSdkVersion project.ext.compileSdkVersion
    buildToolsVersion project.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion project.ext.minSdkVersion
        targetSdkVersion project.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    resourcePrefix "rnuilib_"
}

afterEvaluate {
    android.libraryVariants.all { variant ->
        def variantName = variant.name.capitalize()
        
        tasks.matching { 
            it.name == "generate${variantName}RFile" 
        }.configureEach {
            dependsOn "parse${variantName}LocalResources"
        }
    }
}



dependencies {
    implementation 'com.facebook.react:react-native:+'
}
